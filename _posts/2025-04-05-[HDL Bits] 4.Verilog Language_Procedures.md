---
title: "[HDL Bits] 4.Verilog Language - Procedures"
date: 2025-04-05 19:00:00 +09:00
categories: [Verilog, HDLbits]
tags:
  [
    verilog,
    HDLbits,
  ]
---

# HDL Bits 
HDLBits는 Verilog 하드웨어 설명 언어(HDL)를 사용하여 디지털 하드웨어 설계를 <br>
연습할 수 있는 소규모 회로 설계 연습 문제 모음입니다. <br>
문제가 주어지면 코드를 입력할 수 있고 바로 검증을하여 맞고 틀렸는지 확인할 수 있다. <br>
[HDL Bits](https://hdlbits.01xz.net/wiki/Main_Page)

# 2. Verilog Language 
Verilog Language는 다음과 같은 목차로 구성되어 있다.
- Verilog Language
	- Basics
	- Vectors
	- Modules
	- **Procedures**
	- More_verilog_Features

이번 챕터에서는 `Procedures` 문제를 풀어볼 예정이이다.

## 2.4.1. Alwaysblcoks(combinational) (Alwaysblock1)
### Module Declaration

```verilog
// synthesis verilog_input_version verilog_2001
module top_module(
    input a, 
    input b,
    output wire out_assign,
    output reg out_alwaysblock
);

endmodule
```

이번 문제는 comb 를 구현할 때 `assign` 을 사용할지 `always@(*)` 를 사용할지 연습하는 문제입니다.

둘 다 동작은 동일합니다.



```verilog
// synthesis verilog_input_version verilog_2001
module top_module(
    input a, 
    input b,
    output wire out_assign,
    output reg out_alwaysblock
);

    assign out_assign = a & b;
    
    always@(*) begin
        out_alwaysblock = a & b;
    end
endmodule
```

## 2.4.2. Always blcoks (clocked) (Alwaysblcok2)
### Module Declaration

```verilog
// synthesis verilog_input_version verilog_2001
module top_module(
    input clk,
    input a,
    input b,
    output wire out_assign,
    output reg out_always_comb,
    output reg out_always_ff   );


endmodule
```

문제에서 설명하길 베릴로그에는 세 가지 유형의 할당이 있다고 합니다.

1. 연속 할당 
2. 절차적 (프로시저) 블로킹 할당
3. 절차적 (프로시저) 논블로킹 할당

여기에서 프로시저는 [여기](https://hyeokls.github.io/posts/%EB%B2%A0%EB%A6%B4%EB%A1%9C%EA%B7%B8%EA%B3%B5%EB%B6%80-07.-%ED%96%89%EC%9C%84-%EC%88%98%EC%A4%80-%EB%AA%A8%EB%8D%B8%EB%A7%81/)를 참고하면 이해하는데 도움이 될 수도 있습니다. 


```verilog
// synthesis verilog_input_version verilog_2001
module top_module(
    input clk,
    input a,
    input b,
    output wire out_assign,
    output reg out_always_comb,
    output reg out_always_ff   );

//1.
assign out_assign = a ^ b;

//2.
always @(*) begin
    out_always_comb = a ^ b;
end

//3.
always @(posedge clk) begin
    out_always_ff <= a ^ b;
    
end

endmodule

```


## 2.4.3. If statement (Always if)
### Module Declaration

```verilog
// synthesis verilog_input_version verilog_2001
module top_module(
    input a,
    input b,
    input sel_b1,
    input sel_b2,
    output wire out_assign,
    output reg out_always   ); 

endmodule
```

이 문제는 if 문을 사용할지 조건 연산자를 사용할지에 대해 설명을 해줍니다.

`2:1 mux` 를 구현하는데 sel_b1, sel_b2 모두가 참이면 b를 그렇지 않으면 a를 선택하는 문제입니다.

```verilog
// synthesis verilog_input_version verilog_2001
module top_module(
    input a,
    input b,
    input sel_b1,
    input sel_b2,
    output wire out_assign,
    output reg out_always   ); 

    assign out_assign = (sel_b1 && sel_b2) ? b : a;


    always@(*) begin
        if(sel_b1 && sel_b2) begin
            out_always = b;
        end else begin
            out_always = a;
        end
    end


endmodule
```

## 2.4.4. if statement latches (Always if2)
### Module Declaration

```verilog
// synthesis verilog_input_version verilog_2001
// synthesis verilog_input_version verilog_2001
module top_module (
    input      cpu_overheated,
    output reg shut_off_computer,
    input      arrived,
    input      gas_tank_empty,
    output reg keep_driving  ); //

    always @(*) begin
        if (cpu_overheated)
           shut_off_computer = 1;
    end

    always @(*) begin
        if (~arrived)
           keep_driving = ~gas_tank_empty;
    end

endmodule
```

이번 문제는 래치를 피하는 방법을 알아보는 문제입니다.

1. If (cpu_overheated) then shut_off_computer = 1;
2. If (~arrived) then keep_driving = ~gas_tank_empty;

정말 과열된 경우에만 컴퓨터가 꺼지고 목적지에 도착했거나 주유를 해야 하는 경우 운행을 멈추도록 버그를 수정하세요.

```verilog
// synthesis verilog_input_version verilog_2001
module top_module (
    input      cpu_overheated,
    output reg shut_off_computer,
    input      arrived,
    input      gas_tank_empty,
    output reg keep_driving  ); //

    always @(*) begin
        shut_off_computer = 0; // latch 방지를 위한 선언 
        if (cpu_overheated) begin
           shut_off_computer = 1;
        end 
    end

    always @(*) begin
    keep_driving = 0; //운행을 멈춤
    if (~arrived) // 도착하지 않았다면 , 도착했다면 바로 윗줄
        keep_driving = ~gas_tank_empty; //기름이 비지 않았으면 계속 운행해라
    end 
 
 
 /*   always @(*) begin
        if (arrived || gas_tank_empty)
           keep_driving = 0;
        else begin 
            keep_driving = 1;
        end
    end
*/
endmodule

```



## 2.4.5. case statement (Always case)
### Module Declaration

```verilog
// synthesis verilog_input_version verilog_2001
module top_module ( 
    input [2:0] sel, 
    input [3:0] data0,
    input [3:0] data1,
    input [3:0] data2,
    input [3:0] data3,
    input [3:0] data4,
    input [3:0] data5,
    output reg [3:0] out   );//

    always@(*) begin  // This is a combinational circuit
        case(...)
    end

endmodule

```

이번 문제는 case 문을 활용하여 if-elseif-else 스퀸스를 대체하는 문제입니다.

따라서 이 연습에서는 6 대 1 멀티플렉서를 만듭니다.

셀이 0에서 5 사이이면 해당 데이터 입력을 선택합니다. 

그렇지 않으면 0을 출력합니다. 

데이터 입력과 출력은 모두 4비트 폭입니다.


```verilog
// synthesis verilog_input_version verilog_2001
module top_module ( 
    input [2:0] sel, 
    input [3:0] data0,
    input [3:0] data1,
    input [3:0] data2,
    input [3:0] data3,
    input [3:0] data4,
    input [3:0] data5,
    output reg [3:0] out   );//

    always@(*) begin  // This is a combinational circuit
        case(sel)
        3'b000 : out = data0;
        3'b001 : out = data1;
        3'b010 : out = data2;
        3'b011 : out = data3;
        3'b100 : out = data4;
        3'b101 : out = data5;
        default : out = 0; 
    endcase
    end

endmodule
```


## 2.4.6. Priority encoder (Always case2) 
### Module Declaration 

✯✯✯✰✰

```verilog
// synthesis verilog_input_version verilog_2001
module top_module (
    input [3:0] in,
    output reg [1:0] pos  );

endmodule


```

이번 문제는 우선순위 인코더가 나옵니다.

우선순위 인코더는 입력 비트 벡터가 주어지면 벡터의 첫 번째 1비트 위치를 출력하는 조합회로입니다.

예를 들어, 8비트 우선순위 인코더에 8'b10010000이 입력되면 비트[4]가 가장 높은 첫 번째 비트이므로 3'd4가 출력됩니다.

4비트 우선순위 인코더를 구축합니다. 

이 문제에서는 입력 비트 중 높은 비트가 하나도 없으면(즉, 입력이 0이면) 0을 출력합니다.

4비트 숫자에는 16개의 가능한 조합이 있다는 점에 유의하세요.


```verilog

// synthesis verilog_input_version verilog_2001
module top_module (
    input [3:0] in,
    output reg [1:0] pos  );


    always@(*)begin
        case(in) 
        4'b0000 : pos = 2'b00;
        4'b0001 : pos = 2'b00;
        4'b0010 : pos = 2'b01;
        4'b0011 : pos = 2'b00;
        4'b0100 : pos = 2'b10;
        4'b0101 : pos = 2'b00;
        4'b0110 : pos = 2'b01;
        4'b0111 : pos = 2'b00;
        4'b1000 : pos = 2'b11;
        4'b1001 : pos = 2'b00;  
        4'b1010 : pos = 2'b01; 
        4'b1011 : pos = 2'b00; 
        4'b1100 : pos = 2'b10;
        4'b1101 : pos = 2'b00; 
        4'b1110 : pos = 2'b01; 
        4'b1111 : pos = 2'b00; 
        default : pos = 2'b00; 
        endcase
    end

endmodule

```

## 2.4.7. priority encoder with casez
### Module Declaration

```verilog
// synthesis verilog_input_version verilog_2001
module top_module (
    input [7:0] in,
    output reg [2:0] pos );

endmodule

```

이번 문제는 위와 동일하지만 `casez` 를 사용하여 문제를 더 간단하게 풀 수 있습니다. 


```verilog
// piriority encoder
module top_module (
    input [7:0] in,
    output reg [2:0] pos );

    always@(*)begin
        casez(in)
        8'bzzzzzzz1 : pos = 3'b000;
        8'bzzzzzz1z : pos = 3'b001;
        8'bzzzzz1zz : pos = 3'b010;
        8'bzzzz1zzz : pos = 3'b011;
        8'bzzz1zzzz : pos = 3'b100;
        8'bzz1zzzzz : pos = 3'b101;
        8'bz1zzzzzz : pos = 3'b110;
        8'b1zzzzzzz : pos = 3'b111;
        default: pos = 3'b000;


        endcase
    end

endmodule
```




## 2.4.8 avoding latches (always nolatches)
### Module Declaration

```verilog
// synthesis verilog_input_version verilog_2001
module top_module (
    input [15:0] scancode,
    output reg left,
    output reg down,
    output reg right,
    output reg up  ); 

endmodule
```

이번 문제는 게임용 PS/2 키보드의 스캔 코드를 처리하는 회로를 구축한다고 가정을 합니다.


- Scancode [15:0]	Arrow key
- 16'he06b	left arrow
- 16'he072	down arrow
- 16'he074	right arrow
- 16'he075	up arrow
- Anything else	none

문제에서 말하길 : 

래치를 만들지 않으려면 모든 출력에 가능한 모든 조건의 값을 할당해야한다고 합니다.

단수히 기본값이 있는 것만드로는 충분하지 않고, 네 가지 경우와 기본 케이스 모두에서 네 가지 출력에 모두 값을 할당해야한다고 합니다.

한 가지 가장 쉬운 방법은 `case 문` 앞에 `기본값`을 할당하는 것입니다.

```verilog
// synthesis verilog_input_version verilog_2001
module top_module (
    input [15:0] scancode,
    output reg left,
    output reg down,
    output reg right,
    output reg up  ); 


    always @(*) begin
    up = 1'b0; down = 1'b0; left = 1'b0; right = 1'b0;
        case (scancode)
            16'he06b : left = 1;
            16'he072 : down = 1;
            16'he074 : right = 1;
            16'he075 : up = 1;
            default:  begin left=0; down=0; right=0; up=0; end
        endcase
    end


endmodule
```

---