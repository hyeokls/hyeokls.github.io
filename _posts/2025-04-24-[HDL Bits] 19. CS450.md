---
title: "[HDL Bits] 19.CS450"
date: 2025-04-24 16:05:00 +09:00
categories: [Verilog, HDLbits]
tags:
  [
    verilog,
    HDLbits,
  ]
---

# HDL Bits 
HDLBits는 Verilog 하드웨어 설명 언어(HDL)를 사용하여 디지털 하드웨어 설계를 연습할 수 있는 소규모 회로 설계 연습 문제 모음입니다. 

문제가 주어지면 코드를 입력할 수 있고 바로 검증을 하여 맞고 틀렸는지 확인할 수 있습니다.
[HDL Bits](https://hdlbits.01xz.net/wiki/Main_Page)

# 6. CS450 
**CS450** 는 단일 목차로 구성되어 있습니다.



    
# 공지

오늘은 `CS450` 챕터 4문제를 풀어볼 예정입니다.

모든 문제의 난이도는 `주관적` 으로 설정하였습니다.

문제를 설명하거나 해결할 때 오타나 오류가 발생할 수 있습니다.

이를 참고해주시고 알려주시면 감사하겠습니다.


---

## 6.1. . (CS450/timer)
난이도: ★☆☆☆☆
### Module Declaration
```verilog
module top_module(
	input clk, 
	input load, 
	input [9:0] data, 
	output tc
);
```

<br>

**문제**

주어진 클록 주기 동안 카운트다운한 다음 주어진 기간이 경과했음을 나타내는 신호를 어설트하는 타이머를 구현합니다.

이를 구현하는 좋은 방법은 카운트가 0이 되면 출력 신호를 어설트하는 다운 카운터를 사용하는 것입니다.

각 클록 주기마다:

- `load = 1`이면 타이머가 시간 초과 전에 카운트해야 하는 클록 주기 수인 `10-bit data`를 내부 카운터에 로드합니다. 
    - 카운터가 아직 카운트 중이고 아직 `0`에 도달하지 않은 경우를 포함하여 언제든지 카운터를 로드할 수 있습니다.

- `load = 0`이면 내부 카운터는 `1`씩 감소해야 합니다.

출력 신호 `tc`(“터미널 카운트”)는 내부 카운터가 `0`에 도달했는지 여부를 나타냅니다. 
    
내부 카운터가 `0`에 도달하면 카운터가 다시 로드될 때까지 `0`(카운트 중지)을 유지해야 합니다.

사이트에 첨부된 이미지는 타이머에 3주기 동안 카운트를 요청할 때 어떤 일이 발생하는지 보여주는 예시입니다.

<br>

**해결**

조건에 맞춰 설계를 진행합니다.


<br>

### Write your solution here

```verilog
module top_module(
	input clk, 
	input load, 
	input [9:0] data, 
	output tc
);

reg [9:0] r_data;
reg r_tc;

always@(posedge clk) begin
    if(load) begin
        r_data <= data;
    end else if(r_data == 0) begin
        r_data <= r_data;
    end else begin
        r_data <= r_data - 1'b1;
    end 
end

always@(*)begin
    if(r_data == 0) begin
        r_tc = 1;
    end else begin
        r_tc = 0;
    end
end

assign tc = r_tc;

endmodule
```


<br>

## 6.2. . (Cs450/counter 2bc)
난이도: ★☆☆☆☆
### Module Declaration
```verilog
module top_module(
    input clk,
    input areset,
    input train_valid,
    input train_taken,
    output [1:0] state
);
```

<br>

**문제**

분기 방향 예측기는 프로그램 카운터와 분기 기록으로 인덱싱된 카운터 테이블로 구성하는 경우가 많습니다.

1비트 상태(마지막 결과는 기억)는 히스테리시스가 충분하지 않고 상태가 너무 쉽게 뒤집히기 때문에 각 테이블 항목은 일반적으로 2비트 상태를 사용합니다.

사이트의 이미지를 참고하면 2비트 포화 카운터 상태 다이어그램을 확인할 수 있습니다.

네 가지 상태를 표현하는 것들은  **S**trong, **W**eak, Taken(**T**), Not-Taken(**NT**) 입니다.

상당히 잘 작동하는 2비트 상태머신은 포화 카운터로 최대 3 또는 0까지 카운트하지만 랩핑되지 않습니다.

`taken` 결과는 카운트를 증가시키고, `Not-Taken` 결과는 카운트를 감소시킵니다.

카운트가 2 또는 3(또는 2'b1x)이면 브랜치가 `taken` 한 것으로 예측됩니다.

히스테리시스를 추가하면 강하게 편향된 분기가 가끔 다른 방향으로 갈 때 예측이 뒤집히는 것을 방지할 수 있으며, 예측이 뒤집히기 전에 반대 방향으로 두 번의 증분이 필요합니다.

**Description**
2비트 포화 카운터를 구축합니다.

`train_valid = 1`, `train_taken = 1`일 때 카운터는 증가합니다. (최대 3까지)

`train_valid = 1`, `train_taken = 0`이면 카운터는 감소합니다. (최소 0까지) 

훈련하지 않을 때(`train_valid = 0`) 카운터는 값을 변경하지 않고` 유지`합니다.

`areset`은 `비동기 리셋`으로, 카운터를 WNT(2'b01)로 초기화합니다. 출력 `state[1:0]`은 2비트 카운터 값입니다.

<br>

**해결**


분기 예측(`branch prediction`) 매커니즘에 관한 문제이며 다음과 같이 설명합니다.

**분기예측 설명**

1. 분기 예측기가 테이블 구조를 사용하는 이유
    - 분기 예측기는 프로그램 카운터 값과 과거 분기 기록을 인덱스로 사용하는 카운터 테이블 형태로 구성됩니다.
    - 이런 구조를 통해 프로그램의 특정 지점에서 발생하는 분기 패턴을 학습할 수 있습니다.

2. 2비트 상태를 사용하는 이유
    - 1비트만 사용하면 마지막 분기 결과만 기억하게 됩니다.
        - 이는 히스테리시스(`hysteresis`)가 부족합니다.
            - 히스테리시스란 시스템 이전 상태의 영향을 받는 특성으로, 예측이 쉽게 바뀌지 않도록  안정성을 제공합니다.

    - 1비트 예측기는 분기 패턴이 `"TNTNTNT..."` 처럼 교대로 나타날 때 예측이 틀리게 됩니다.
    - 2비트를 사용하면 가끔 다른 방향으로 분기되더라도 예측이 쉽게 바뀌지 않습니다.

**2비트 포화 카운터 동작 원리**

첨부된 이미지를 확인하면 4가지의 상태가 있습니다. 

- `SNT(00)` = Strong Not-Taken : 강하게 분기하지 않을 것으로 예측됨

- `WNT(01)` = Weak Not- Taken : 약하게 분기하지 않을 것으로 예측됨

- `WT(10)` = Weak Taken : 약하게 분기할 것으로 예측됨

- `ST(11)` = Strong Taken : 강하게 분기될 것으로 예측됨

이 관계는 `taken = 1`이면 카운터 증가 `taken = 0`이면 카운트 감소의 형태를 보입니다.


<br>

### Write your solution here
```verilog
module top_module(
    input clk,
    input areset,
    input train_valid,
    input train_taken,
    output [1:0] state
);

reg [1:0] r_state;

always@(posedge clk, posedge areset) begin
    if(areset) begin
        r_state <= 2'b01;
    end else if(train_valid) begin
        if(train_taken) begin
            r_state <= (r_state == 2'b11) ? 2'b11 : r_state + 1'b1;
        end else begin
            r_state <= (r_state == 2'b00) ? 2'b00 : r_state - 1'b1;
        end
    end
end

assign state = r_state;

endmodule
```

<br>

## 6.3. . (Cs450/history shift)
난이도: ★★★☆☆
### Module Declaration
```verilog
module top_module(
    input clk,
    input areset,

    input predict_valid,
    input predict_taken,
    output [31:0] predict_history,

    input train_mispredicted,
    input train_taken,
    input [31:0] train_history
);
```

<br>

**문제**

분기 방향 예측기는 프로그램 카운터와 `branch history`로 인덱싱된 카운터 테이블로 구성되는 경우가 많습니다.

분기 기록은 최근 분기에서 `taken` 또는 `Not taken`의 결과의 스퀸스입니다.

하드웨어에서 `branch history register`는 `N-bit shift register`로 구현할 수 있습니다.

각 조건부 분기 방향이 예측되면 예측된 방향이 시프트 레지스터로 이동합니다.

따라서 시프트 레지스터는 `가장 최근의 N 개의 분기 결과`를 보유합니다.

> 이미지를 참고하면 주변 하드웨어가 있는 `branch history register` 를 보여줍니다.
이 문제에서는 파란색 점선 사각형 안에 있는 `branch histroy register`를 구축합니다.
이 다이어그램은 브랜치 실행 유닛이 브랜치 오예측을 알리는 것을 보여 주지만, 프로세서 설계에 따라 이 시점은 폐기 또는 다른 시점이 될 수도 있습니다.

분기 예측이 추측적으로 수행되기 때문에 `pipeline flushes`로 인해 추가적인 복잡성이 발생합니다.

브랜치 오예측이 발생하면 프로세서 상태를 오예측된 브랜치 직후의 상태로 롤백해야 합니다.

여기에는 잘못 예측된 브랜치보다 최신 분기 에 의해 시프트 인되었지만 이제 폐기해야 하는 예측된 브랜치 결과를 포함할 수 있는 글로벌 히스토리 레지스터를 롤백하는 것이 포함됩니다.

여기서는 분기 예측기 외부에 각 분기를 예측하는 데 사용된 분기 히스토리 레지스터의 상태를 기억하는 하드웨어가 있으며, 나중에 분기 예측기 학습 및 파이프라인 플러시에 사용하기 위해 저장되어 있다고 가정합니다. 

브랜치 오예측이 발생하면 이 하드웨어는 브랜치 예측기에 브랜치가 오예측되었다는 사실과 브랜치가 취했어야 하는 방향, 그리고 오예측된 브랜치 바로 앞의 프로그램 지점에 해당하는 브랜치 히스토리 레지스터의 상태를 알려줍니다.

물론 프로세서가 잘못 예측된 분기 이후 지점으로 다시 시작되므로 파이프라인 플러시 후 분기 이력 레지스터에는 잘못 예측된 분기의 실제 방향이 추가되어야 합니다.

**Description**
분기 오예측으로 인한 파이프라인 플러시에 대한 응답으로 롤백 상태를 지원하는 `32-bit global history shift register`를 구축합니다.

`branch prediction`이 이루어지면(`predict_valid = 1`) LSB 쪽에서 `predict_taken`을 쉬프트하여 예측된 분기에 대한 분기 기록을 업데이트합니다. (predict_history[0] is the direction of the youngest branch.)

분기 오예측이 발생하면(`train_mispredicted = 1`), 오예측된 분기가 완료된 후의 기록이 있는 `branch history register` 를 로드합니다.

이것은 오예측된 분기 이전의 이력(`train_history`)과 실제 분기 결과(`train_taken`)를 연결한 이력입니다.

예측과 오예측이 동시에 발생하는 경우, 파이프라인 플러시는 현재 예측중인 브랜치도 플러시하기 때문에 오예측이 우선시됩니다.

`predict_history`는 `branch history register`의 값입니다.

`areset`은 히스토리 카운터를 0으로 초기화하는 비동기 리셋입니다.


<br>

**해결**

>`파이프라인 플러시`는 컴퓨터 프로세서의 파이프라인 구조에서 잘못된 분기 예측이나 예외 상황이 발생했을 때, 파이프라인에 이미 들어와 처리 중이던 명령어들을 모두 무효화하고, 파이프 라인을 비우는 작업을 말합니다.


<br>

### Write your solution here
```verilog
module top_module(
    input clk,
    input areset,

    input predict_valid,
    input predict_taken,
    output [31:0] predict_history,

    input train_mispredicted,
    input train_taken,
    input [31:0] train_history
);

reg [31:0] history;

always@(posedge clk , posedge areset) begin
    if(areset) begin
        history <= 32'b0;
    end else if(train_mispredicted) begin
        history <= {train_history[30:0], train_taken};
    end else if(predict_valid) begin
        history <= {history[30:0], predict_taken};
    end
end

    assign predict_history = history;
endmodule
```

<br>


## 6.4. . (Cs450/gshare)
난이도: ★★☆☆☆
### Module Declaration
```verilog
module top_module(
    input clk,
    input areset,

    input  predict_valid,
    input  [6:0] predict_pc,
    output predict_taken,
    output [6:0] predict_history,

    input train_valid,
    input train_taken,
    input train_mispredicted,
    input [6:0] train_history,
    input [6:0] train_pc
);
```

<br>

**문제**

분기 방향 예측기
분기 방향 예측기는 조건부 분기 명령어의 방향에 대해 `taken`/ `Not-taken`  예측을 생성합니다.

프로세서 파이프라인의 가장 앞쪽에 위치하며, 명령어 가져오기를 (올바른 프로그램 실행 경로로) 유도하는 역할을 합니다. 

분기 방향 예측기는 일반적으로 분기 대상 버퍼(BTB)와 함께 사용되며, BTB는 대상 주소를 예측하고 방향 예측기는 대상에 분기할지 아니면 `fall-through path`를 따라 계속 페치할지를 선택합니다.

파이프라인의 후반부(일반적으로 분기 실행 또는 리타이어 시)에 실행된 분기 명령의 결과가 분기 예측기로 다시 전송되어 과거의 분기 동작을 관찰하여 미래를 더 정확하게 예측하도록 훈련시킵니다. 

잘못 예측된 브랜치가 있는 경우 파이프라인 플러시가 발생할 수도 있습니다.

사이트에 이미지를 참고하면 분기 방향 예측기는 `Fetch stage`에 있습니다.

분기 예측기는 현재 `PC`와 `History reigster`를 사용하여 예측을 수행하며, 예측 결과는 다음 `PC` 값에 영향을 미칩니다.

학습 및 오예측 요청은 파이프라인의 후반부에서 이루어집니다.

이 문제에서는 분기 방향 예측기가 다이어그램에 표시된 가상의 프로세서 파이프라인의 `Fetch` 단계에 있는 것으로 가정합니다.

이 문제에서는 다이어그램에서 파란색 점선 사각형으로 표시된 분기 방향 예측자만 생성합니다.

반대로 패턴 히스토리 테이블(PHT)과 `branch history register`의 업데이트는 플리플롭에 저장된 상태의 경우와 마찬가지로 다음 양의 클록 에지에서 적용됩니다.

**Gshare predictor**

분기 방향 예측기는 프로그램 카운터와 분기 기록에 의해 색인된 카운터 테이블로 구성되는 경우가 많습니다.

테이블 인덱스는 분기 주소와 히스토리의 해시이며, 각 분기와 히스토리 조합에 고유한 테이블 항목을 부여하려고 시도합니다(또는 최소한 충돌 횟수를 줄이려고 시도합니다).

각 테이블 항목에는 과거에 동일한 분기 및 히스토리 패턴이 실행되었을 때 분기 방향을 기억하기 위한 2비트 포화 카운터가 포함되어 있습니다.

이러한 스타일의 예측자 중 한 가지 예가 gshare 예측기입니다.

gshare기알고리즘에서 분기 주소(PC)와 히스토리 비트는 테이블 인덱스 비트를 '공유'합니다.

기본 gshare 알고리즘은 N개의 분기기주소 비트와 N개의 글로벌 분기 기록 비트를 함께 소싱하여 N비트 PHT 테이블 인덱스를 계산합니다.

그런 다음 N비트 인덱스는 2비트 포화 카운터로 구성된 2N 엔트리 테이블의 한 항목에 액세스하는 데 사용됩니다.

이 카운터의 값은 예측을 제공합니다(0 또는 1 = 취하지 않음, 2 또는 3 = 취함).

학습은 비슷한 방식으로 테이블을 색인화합니다.

학습용 PC와 히스토리는 테이블 인덱스를 계산하는 데 사용됩니다.

그런 다음 해당 인덱스의 2비트 카운터는 분기의 실제 결과에 따라 증가하거나 감소합니다.

**Description**

`7-bit PC` 및 `7-bit global history`를 `7-bit` 인덱스로 해시(`xor` 사용)한 gshare 분기 예측기를 구축합니다. 

이 인덱스는 `2-bit saturating counters`로 구성된 `128개 항목의 테이블`에 액세스합니다(cs450/counter_2bc와 유사). 

분기 예측기에는 ` 7-bit global branch history register`가 포함되어야 합니다(cs450/history_shift와 유사).

분기 예측기에는 두 세트의 인터페이스가 있습니다: 
- 하나는 예측을 위한 것이고 다른 하나는 학습을 위한 것입니다.
 
예측 인터페이스는 프로세서의 가져오기 단계에서 분기 예측기에 가져오는 명령어에 대한 분기 방향 예측을 요청하는 데 사용됩니다.

이러한 브랜치가 파이프라인을 따라 진행되어 실행되면 브랜치의 실제 결과를 알 수 있습니다.

그런 다음 분기 예측기는 실제 분기 방향 결과를 사용하여 학습됩니다.

주어진 PC에 대해 분기 예측이 요청되면(`predict_valid = 1`), 분기 예측기는 예측에 사용된 분기 히스토리 레지스터의 예측된 분기 방향과 상태를 생성합니다. 

그런 다음 예측된 분기에 대해 분기 이력 레지스터가 업데이트됩니다(다음 양의 클럭 에지에서).

분기에 대한 학습이 요청되면(`train_valid = 1`), 분기 예측자에게 학습 중인 분기에 대한 `PC` 및 분기 이력 레지스터 값과 함께 실제 분기 결과 및 해당 분기가 잘못된 예측인지(파이프라인 플러시 필요) 여부가 알려집니다. 패턴 이력 테이블(PHT)을 업데이트하여 다음 번에 이 브랜치를 더 정확하게 예측하도록 브랜치 예측기를 학습시킵니다.

또한, 학습 중인 브랜치가 잘못 예측된 경우, 잘못 예측된 브랜치가 실행을 완료한 직후에 브랜치 이력 레지스터를 상태로 복구하세요.

주어진 PC에 대해 분기 예측이 요청될 때(`predict_valid = 1`), 잘못된 예측에 대한 훈련과 (다른 하위 명령어에 대한) 예측이 같은 주기에 발생하는 경우, 두 작업 모두 분기 기록 레지스터를 수정하려고 합니다. 

이 경우 예측 중인 브랜치는 어쨌든 폐기되므로 학습이 우선합니다.

동일한 PHT 항목에 대한 훈련과 예측이 동시에 발생하는 경우, 훈련은 다음 양의 클럭 에지에서만 PHT를 수정하므로 예측은 훈련 전에 PHT 상태를 확인합니다.

다음 타이밍 다이어그램은 `PHT` 엔트리 `0`을 동시에 학습하고 예측할 때의 타이밍을 보여줍니다.

사이클 4의 트레이닝 요청은 사이클 5의 PHT 엔트리 상태를 변경하지만 사이클 4의 예측 요청은 사이클 4의 트레이닝 요청의 효과를 고려하지 않고 
사이클 4의 PHT 상태를 출력합니다.

<br>


**해결**

문제 설명이 정말 길었습니다.

문제를 요약해보겠습니다.

1. `predict_valid =1 ` 일때 predict_pc ^ history

2. PHT에서 해당 인덱스의 2비트 카운터 값 체크
    - `0`,`1` : not-taken 예측
    - `2`,`3` : taken 예측

3. 예측 결과 출력 : `predict_taken`, `predict_history`

4. 다음 클럭에서 업데이트 : `{history[5:0], predict_taken}`

5. 값 업데이트는 `train_taken`
    - `1` : 최대 3까지 카운트 증가
    - `0` : 최소 0까지 카운트 감소

6. 잘못 예측일 경우(`train_mispredicted = 1`) 
    - 복구 : `{train_history[5:0],train_taken}`

7. 학습, 예측 우선순위 
    - 학습이 우선

8. 초기화 
    - 모든 PHT : `01`로 초기화
    - `history `레지스터 초기화

<br>

### Write your solution here
```verilog
module top_module(
    input clk,
    input areset,

    input  predict_valid,
    input  [6:0] predict_pc,
    output predict_taken,
    output [6:0] predict_history,

    input train_valid,
    input train_taken,
    input train_mispredicted,
    input [6:0] train_history,
    input [6:0] train_pc
);

reg [1:0] PHT [0:127];
reg [6:0] history;

wire [6:0] w_pre_index = predict_pc ^ history;
wire [6:0] w_train_index = train_pc ^ train_history;


integer i;

always@(posedge clk, posedge areset) begin
    if(areset) begin
        for(i = 0; i< 128; i++) begin
            PHT[i] <= 2'b01;
        end

    end else begin
        if(train_valid) begin
            if(train_taken) begin
                PHT[w_train_index] <= (PHT[w_train_index] == 2'b11) ? 2'b11 : PHT[w_train_index] + 1'b1;
            end else begin
                PHT[w_train_index] <= (PHT[w_train_index] == 2'b00) ? 2'b00 : PHT[w_train_index] - 1'b1;
            end
        end
    end
end
        
always@(posedge clk, posedge areset) begin
    if(areset) begin
        history <= 7'b0;
    end else begin
        if(train_valid && train_mispredicted) begin
            history <= {train_history[5:0],train_taken};
        end else if(predict_valid) begin
            history <= {history[5:0],predict_taken};
        end
    end
end

assign predict_taken = PHT[w_pre_index][1];
assign predict_history = history;

endmodule
```



#### 끝

---