---
title: "[HDL Bits] 11.Circuits - Seq Logic - Counters"
date: 2025-04-09 23:20:00 +09:00
categories: [Verilog, HDLbits]
tags:
  [
    verilog,
    HDLbits,
  ]
---

# HDL Bits 
HDLBits는 Verilog 하드웨어 설명 언어(HDL)를 사용하여 디지털 하드웨어 설계를 연습할 수 있는 소규모 회로 설계 연습 문제 모음입니다. 

문제가 주어지면 코드를 입력할 수 있고 바로 검증을 하여 맞고 틀렸는지 확인할 수 있습니다.
[HDL Bits](https://hdlbits.01xz.net/wiki/Main_Page)

# 2. Circuits 
Circuits 는 다음과 같은 목차로 구성되어 있다.
- Circuits
	- Combinational Logic
        - Basic Gates
        - Multiplexers
        - Arithmetic Circuits
        - Karnaugh Map to Circuit
    - Sequential Logic
        - Latches and Flip-Flops
        - **Counters**
        - Shift Registers
        - More Circuts
        - Finite State Machines
    - Building Larger Circuits
    

오늘은 이 중 `Sequential Logic` 챕터의  `Counters` 를 풀어볼 예정이이다.

모든 문제의 난이도는 `주관적` 으로 설정하였습니다.

---

## 3.4.2.1. Four-bit binary counter
### Module Declaration
난이도: ★☆☆☆☆
```verilog
module top_module (
    input clk,
    input reset,      // Synchronous active-high reset
    output [3:0] q);

endmodule

```

<br>

이번 문제는 4비트 카운터를 설계하는 문제입니다.

<br>

```verilog
module top_module (
    input clk,
    input reset,      // Synchronous active-high reset
    output [3:0] q);


    wire w_is_done;

    assign w_is_done = (q == 4'hf); 
    always@(posedge clk) begin
        if(reset) begin
            q <= 0;
        end else if (w_is_done) begin
            q <= 0;
        end else begin 
            q <= q + 1'b1;
        end
    end
endmodule

```

## 3.4.2.2. Decade counter (Count10)
### Module Declaration
난이도:  ★☆☆☆☆
```verilog
module top_module (
    input clk,
    input reset,        // Synchronous active-high reset
    output [3:0] q);

endmodule

```

<br>

이번 문제는 10 까지 카운팅을 진행합니다.

<br>

```verilog
module top_module (
    input clk,
    input reset,      // Synchronous active-high reset
    output [3:0] q);


    wire w_is_done;

    assign w_is_done = (q == 4'h9); 
    always@(posedge clk) begin
        if(reset) begin
            q <= 0;
        end else if (w_is_done) begin
            q <= 0;
        end else begin 
            q <= q + 1'b1;
        end
    end
endmodule
```


## 3.4.2.3. Decade counter again (Count1to10)
### Module Declaration
난이도:  ★☆☆☆☆
```verilog
module top_module (
    input clk,
    input reset,
    output [3:0] q);

endmodule

```

<br>

이번 문제는 리셋시 1 부터 시작합니다.

<br>

```verilog
module top_module (
    input clk,
    input reset,     
    output [3:0] q);


    wire w_is_done;

    assign w_is_done = (q == 4'b1010); 
    always@(posedge clk) begin
        if(reset) begin
            q <= 1;
        end else if (w_is_done) begin
            q <= 1;
        end else begin 
            q <= q + 1'b1;
        end
    end
endmodule
```

## 3.4.2.4. Slow decade counter Countslow
### Module Declaration
난이도:  ★★☆☆☆
```verilog
module top_module (
    input clk,
    input slowena,
    input reset,
    output [3:0] q);

endmodule

```
<br>

이번 문제는 10 카운트 , 동기리셋 0 이며 slowna 가 1일 때 다음 카운트로 넘어가는 문제입니다. 

<br>

```verilog
module top_module (
    input clk,
    input slowena,
    input reset,
    output [3:0] q);
   
    wire w_is_done;

    assign w_is_done = (q == 4'h9); 
    always@(posedge clk) begin
        if(reset) begin
            q <= 0;
        end else if (w_is_done && slowena) begin
            q <= 0;
        end else if (slowena) begin 
            q <= q + 1'b1;
        end else begin
            q <= q;
        end
    end
endmodule

```


## 3.4.2.5. Counter 1-12 (Exmas/ece241 2014 q7a)
### Module Declaration
난이도:  ★★★★☆
```verilog
module top_module (
    input clk,
    input reset,
    input enable,
    output [3:0] Q,
    output c_enable,
    output c_load,
    output [3:0] c_d
); //

    count4 the_counter (clk, c_enable, c_load, c_d /*, ... */ );

endmodule

```

<br>

이번 문제는 여러 조건들이 있습니다.

1. `Reset` : Synchronous active-high reset that forces the count to 1

2. `Enable` : Set high for the counter to run

3. `clk` : Pos edge-triggerd clock input

4. `Q[3:0]` : The output of the counter

5. `c_enable` , `c_load`, `c_d[3:0]` : Control signals going to the provided 4-bit counter, so correct operation can be verified

사용 가능한 구성요소는 다음과 같습니다 :

1. 아래에 4비트 바이너리 카운터에는 `Enable` 및 동기식 병렬 부하가 있습니다. (Enable 보다 우선 순위가 높음)

    ```verilog
    module count4(
        input clk,
        input enable,
        input load,
        input [3:0] d,
        output reg [3:0] Q
    );
    ```

2. `c_enable`, `c_load` 및 `c_d` 출력은 각각 내부 카운터의 `enable`, `load` 및 `d` 입력으로 전달되는 신호입니다. 이들의 목적은 이러한 신호가 올바른지 확인하는 것입니다.


주어진 `count4`를 확인해보면 다음과 같이 코어가 구성되어 있다는 것을 짐작할 수 있습니다.

```verilog
always@(posedge clk) begin
    if(load) begin
        Q <= d;
    end else if(enable) begin
        Q <= Q + 1;
    end 
end

```

이번 문제는 `input` 값으로 받은 상태들을 `output`으로 보내고 이를 인스턴스 모듈에 적용시키는 것입니다.

<br>

```verilog
module top_module (
    input clk,
    input reset,
    input enable,
    output [3:0] Q,
    output c_enable,
    output c_load,
    output [3:0] c_d
); 
    count4 u_count4(clk, c_enable , c_load, c_d ,Q);

    assign c_enable = enable && ~c_load;

    assign c_load = reset || (Q == 12 && enable == 1);
    
    assign c_d = (c_load) ? 4'b0001:4'b000;
endmodule

```

<br>

1. `c_enable` 신호는 `enable` 신호가 `1` 이면서 `c_load` 가 `0` 이여야 합니다.
    - `load` 가 `1` 이면 `reset` 되는 것이기 때문입니다.

2. `c_load` 신호는 `reset` 과 함께 카운터가 초기화 되는 조건을 `||` 연산자로 묶었습니다.

3. `c_d` 신호는 `c_load` 신호가 들어오면 조건에 따라 `1`로 설정해야합니다.

<br>

## 3.4.2.6. Counter 1000 (Exams/ece241 2014 q7b)
### Module Declaration
난이도:  ★★★☆☆
```verilog
module top_module (
    input clk,
    input reset,
    output OneHertz,
    output [2:0] c_enable
); //

    bcdcount counter0 (clk, reset, c_enable[0]/*, ... */);
    bcdcount counter1 (clk, reset, c_enable[1]/*, ... */);

endmodule

```
<br>

이번 문제는 1000hz 벽시계를 제작하기 위해 시/분/초 캉누터 세트의 활성화 신호를 구동하는 데 사용할 수 있는 1Hz를 만들어야 합니다.

기본으로 제공하본 모듈입니다.

<br>

```verilog
module bcdcount (
	input clk,
	input reset,
	input enable,
	output reg [3:0] Q
);
```
<br>

 3개를 인스턴스화 하여 각각 10씩 카운트 총 1000 카운트를 진행하는 문제입니다.

<br>

```verilog
module top_module (
    input clk,
    input reset,
    output OneHertz,
    output [2:0] c_enable
); 
   
    wire [3:0] w_Q0, w_Q1, w_Q2;
    assign c_enable[0] =  1'b1;
    assign c_enable[1] = (w_Q0 == 4'h9 );
    assign c_enable[2] = (w_Q0 == 4'h9 && w_Q1 == 4'h9);

    bcdcount counter0 (clk, reset, c_enable[0],w_Q0);
    bcdcount counter1 (clk, reset, c_enable[1],w_Q1);
    bcdcount counter2 (clk, reset, c_enable[2],w_Q2);

    assign OneHertz = (w_Q0 == 4'h9 && w_Q1 == 4'h9 && w_Q2 == 4'h9);
endmodule

```

## 3.4.2.7. 4-digit decimal counter (Countbcd)
### Module Declaration
난이도: ★★★☆☆
```verilog
module top_module (
    input clk,
    input reset,   // Synchronous active-high reset
    output [3:1] ena,
    output [15:0] q);

endmodule

```

<br>

이번 문제는 BCD 카운터를 구현합니다.

한자리 수 BCD 카운터를 인스턴스하거나 수정할 수 있습니다.

<br>

```verilog
module top_module (
    input clk,
    input reset,   // Synchronous active-high reset
    output [3:1] ena,
    output [15:0] q);

cnt_bcd u_cnt_bcd_0(clk,reset,1,q[3:0]);
cnt_bcd u_cnt_bcd_1(clk,reset,ena[1],q[7:4]);
cnt_bcd u_cnt_bcd_2(clk,reset,ena[2],q[11:8]);
cnt_bcd u_cnt_bcd_3(clk,reset,ena[3],q[15:12]);
   
assign ena[1] = (q[3:0] == 4'b1001);           
assign ena[2] = (q[7:4] == 4'b1001) && ena[1]; 
assign ena[3] = (q[11:8] == 4'b1001) && ena[2]; 

endmodule

module cnt_bcd(
    input clk,
    input reset,
    input ena,
    output [3:0] q
);
    reg [3:0] w_q;
    always@(posedge clk) begin
        if(reset) begin
            w_q <= 1'b0;
        end else if (ena) begin
            if(w_q == 4'b1001) begin
                w_q <= 4'b0000;
            end else begin
                w_q <= w_q + 4'b0001;
            end
        end
    end
    assign q = w_q;
endmodule
```

## 3.4.2.8. 12-hour clock (Count clock)
### Module Declaration
난이도: ★★★★☆
```verilog
module top_module(
    input clk,
    input reset,
    input ena,
    output pm,
    output [7:0] hh,
    output [7:0] mm,
    output [7:0] ss); 

endmodule

```

<br>

이번 문제는 오전 오후 기능이 있는 시계를 설계합니다.

1. `reset` 시 오전 12:00 로 초기화 됩니다.

2. pm 은 오전인 경우 `0` 오후인 경우 `1` 입니다.

3. 시 분 초는 BCD 이고 `reset` 은 `ena` 보다 우선순위가 높으며 활성화하지 않은 상태에서도 발생할 수 있습니다.


시계를 설계할 때 다음을 고려해야 합니다.

1. 오후 11:59:59 -> 오전 12:00:00

2. 오전 12:59:59 -> 오전 01:00:00

3. 오전 11:59:59 -> 오후 12:00:00

4. 오후 12:00:00 -> 오후 01:00:00

<br>

```verilog
module top_module(
    input clk,
    input reset,
    input ena,
    output pm,
    output [7:0] hh,
    output [7:0] mm,
    output [7:0] ss); 


    always@(posedge clk) begin
        if(reset) begin
            ss <= 8'h00;
        end else if(ena) begin
            if(ss == 8'h59)begin
                ss <= 8'h00;
            end else if(ss[3:0] == 8'h09) begin
                ss[7:4] <= ss[7:4] + 4'b0001;
                ss[3:0] <= 4'b0000;
            end else begin
                ss[3:0]  <= ss[3:0] + 4'b0001; 
            end
        end
    end

    always@(posedge clk) begin
        if(reset) begin
            mm <= 8'h00;
        end else if(ena && ss == 8'h59) begin
            if(mm == 8'h59)begin
                mm <= 8'h00;
            end else if(mm[3:0] == 8'h09) begin
                mm[7:4] <= mm[7:4] + 4'b0001;
                mm[3:0] <= 4'b0000;
            end else begin
                mm[3:0]  <= mm[3:0] + 4'b0001; 
            end
        end
    end   

    always@(posedge clk) begin
        if(reset) begin
            pm <= 0;
            hh <= 8'h12;
        end else if(ena && ss == 8'h59 && mm == 8'h59) begin
            if(hh == 8'h11) begin
                pm <= ~pm;
                hh <= 8'h12;
            end else if (hh == 8'h12) begin
                hh <= 8'h01;
            end else if (hh[3:0] == 8'h09) begin
                hh[7:4] <= hh[7:4] + 4'b0001;
                hh[3:0] <= 4'b0000;
            end else begin
                hh[3:0]  <= hh[3:0] + 4'b0001; 
            end
        end
    end

endmodule

```


#### 다음에 계속

---