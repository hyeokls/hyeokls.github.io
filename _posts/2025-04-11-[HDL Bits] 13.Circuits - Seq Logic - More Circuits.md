---
title: "[HDL Bits] 13.Circuits - Seq Logic - More Circuits"
date: 2025-04-11 01:30:00 +09:00
categories: [Verilog, HDLbits]
tags:
  [
    verilog,
    HDLbits,
  ]
---

# HDL Bits 
HDLBits는 Verilog 하드웨어 설명 언어(HDL)를 사용하여 디지털 하드웨어 설계를 연습할 수 있는 소규모 회로 설계 연습 문제 모음입니다. 

문제가 주어지면 코드를 입력할 수 있고 바로 검증을 하여 맞고 틀렸는지 확인할 수 있습니다.
[HDL Bits](https://hdlbits.01xz.net/wiki/Main_Page)

# 2. Circuits 
Circuits 는 다음과 같은 목차로 구성되어 있습니다.
- Circuits
	- Combinational Logic
        - Basic Gates
        - Multiplexers
        - Arithmetic Circuits
        - Karnaugh Map to Circuit
    - Sequential Logic
        - Latches and Flip-Flops
        - Counters
        - Shift Registers
        - **More Circuits**
        - Finite State Machines
    - Building Larger Circuits
    
# 공지

오늘은 이 중 `Sequential Logic` 챕터의  `More Circuits` 를 풀어볼 예정입니다.

모든 문제의 난이도는 `주관적` 으로 설정하였습니다.

문제를 설명하거나 해결할 때 오타나 오류가 발생할 수 있습니다.

이를 참고해주시고 알려주시면 감사하겠습니다.

---

## 3.4.4.1. Rule90 (Rule90)
### Module Declaration
난이도: ★★★☆☆
```verilog
module top_module(
    input clk,
    input load,
    input [511:0] data,
    output [511:0] q ); 

endmodule
```

<br>

이번 문제는 설명과 표를 참고하여 진행합니다.

`Rule 90` 은 1차원 `cellular automaton` 입니다.

규칙은 간단합니다. 셀의 1차원 배열(`on` 또는 `off`)이 있습니다.

각 스텝에서 각 셀의 다음 상태는 현재 셀의 두 이웃의 `XOR` 입니다. 

이 규칙을 좀 더 자세히 표현하는 방법은 다음 표에서 셀의 다음 상태가 자신과 두 이웃의 함수인 경우입니다 :


|LEFT|CENTER|RIGHT|Center's next state|
|----|------|-----|-------------------|
| 1  |   1  |  1  |        0          |
| 1  |   1  |  0  |        1          |
| 1  |   0  |  1  |        0          |
| 1  |   0  |  0  |        1          |
| 0  |   1  |  1  |        1          |
| 0  |   1  |  0  |        0          |
| 0  |   0  |  1  |        1          |
| 0  |   0  |  0  |        0          |

이 회로에서 512 셀 시스템('q[511:0]')을 생성하고 매 클록 주기마다 한 타임 스텝씩 전진합니다.

부하 입력은 시스템의 상태가 `data[511:0]`으로 `load`되어야함을 나타냅니다.

경계(`q[-1]` 및 `q[512]`)가 모두 `0(off)` 라고 가정합니다.

<br>

```verilog
module top_module(
    input clk,
    input load,
    input [511:0] data,
    output [511:0] q ); 

    reg [511:0] state;

    always@(posedge clk) begin
        if(load) begin
            state <= data;
        end else begin
            state <= {1'b0 ^ state[510] ,state[511:2] ^ state[509:0], state[1] ^1'b0};
        end
    end

    assign q = state;

endmodule

```



## 3.4.4.2. Rule110 (Rule110)
### Module Declaration
난이도:  ★★★★☆
```verilog
module top_module(
    input clk,
    input load,
    input [511:0] data,
    output [511:0] q
); 

endmodule


```

<br>

이번 문제는 설명과 표를 참고하여 진행합니다.

`Rule 110` 은 1차원 `cellular automaton` 입니다.

규칙은 간단합니다. 셀의 1차원 배열(`on` 또는 `off`)이 있습니다.

`Rule 110`에서 각 셀의 다음 상태는 다음 표에 따라 자신과 두 개의 이웃 셀만 의존합니다:

|LEFT|CENTER|RIGHT|Center's next state|
|----|------|-----|-------------------|
| 1  |   1  |  1  |        0          |
| 1  |   1  |  0  |        1          |
| 1  |   0  |  1  |        1          |
| 1  |   0  |  0  |        0          |
| 0  |   1  |  1  |        1          |
| 0  |   1  |  0  |        1          |
| 0  |   0  |  1  |        1          |
| 0  |   0  |  0  |        0          |

이 회로에서 512 셀 시스템('q[511:0]')을 생성하고 매 클록 주기마다 한 타임 스텝씩 전진합니다.

부하 입력은 시스템의 상태가 `data[511:0]`으로 `load`되어야함을 나타냅니다.

경계(`q[-1]` 및 `q[512]`)가 모두 `0(off)` 라고 가정합니다.

<br>

```verilog
module top_module(
    input clk,
    input load,
    input [511:0] data,
    output [511:0] q
); 

    reg [511:0] state;
    integer i;
    always@(posedge clk) begin
        if(load) begin
            state <= data;
        end else begin
            state[511] <= (state[511] | state[510]);
            state[0] <= state[0];

            for(i=1; i<511; i++) begin
                case({state[i+1],state[i],state[i-1]})
                    3'b000 : state[i] <= 0;
                    3'b001 : state[i] <= 1;
                    3'b010 : state[i] <= 1;
                    3'b011 : state[i] <= 1;
                    3'b100 : state[i] <= 0;
                    3'b101 : state[i] <= 1;
                    3'b110 : state[i] <= 1;
                    3'b111 : state[i] <= 0;
                endcase
            end
        end 
    end

    assign q = state;

endmodule
```


## 3.4.4.3. Conway's Game of Life 16 x 16 (Conwaylife)
### Module Declaration
난이도:  ★★★★★
```verilog
module top_module(
    input clk,
    input load,
    input [255:0] data,
    output [255:0] q ); 

endmodule
```

<br>

이번 문제는 `conway's Game of Life` 이며 이는 2차원 `cellular automaton` 입니다.

이 `게임`은 2차원 격자의 `cells` 에서 진행되며, 각 `cell`은 `1(alive)` or `0(dead)` 입니다.

각 단계에서 각 `cell`은 얼마나 많은 이웃을 가지고 있는지에 따라 상태가 바뀝니다.:

1. 이웃이 0-1 : 셀이 0이 됩니다.

2. 이웃이 2: 셀 상태가 변경되지 않습니다.

3. 이웃이 3: 셀이 1이 됩니다.

4. 이웃이 4 이상: 셀은 0이 됩니다.

이 게임은 무한 그리드에 대해 공식화되었습니다.

이 회로에서는 `16 x 16` 그리드를 사용합니다.

변이 그리드의 반대쪽을 감싸고 있습니다.

q[15:0] : 첫 번째 행 (행 0번) 의 16개의 셀

`load`: 초기 상태 로딩을 위해 다음 클럭 에지에서 데이터를 q로 로드합니다.

`q`: 매 클럭 주기마다 업데이트되는 16x16의 현재 게임 상태입니다.

게임 상태는 매 클록 사이클마다 한 타임스텝씩 진행되어야 합니다.

<br>

#### 문제 분석 

문제의 설명을 한 번 분석해보곘습니다.

Q. 예를들어 모서리 셀(0,0)의 이웃은 (15,1), (15,0), (15,15), (0,1), (0,15), (1,1), (1,0), (1,15) 등 8개입니다.

A. 이 조건을 시각적으로 표현을 하면 다음과 같게 됩니다.

<br>

(15,15) | (15,0) | (15,1)
--------+--------+--------
(0,15)  | (0,0)  | (0,1)
--------+--------+--------
(1,15)  | (1,0)  | (1,1)

<br>

Q. 6x16 그리드는 길이 256 벡터로 표현되며, 16개 셀의 각 행은 하위 벡터로 표현됩니다(예: q[15:0]은 행 0, q[31:16]은 행 1 등). 

A. 이 조건을 해석하면 다음과 같습니다.




1. 2차원 좌표를 1차원 벡토 인덱스로 변환하기 위해 다음과 같은 공식을 사용합니다.
    - index = row * 16 + col
    - ex :
        - (0,0)은 q[0] 에 저장
        - (7,8)은 q[7*16+8] = q[120] 에 저장
        - (15,15)는 q[15*16+15] = q[255]에 저장
    - 이런식으로 0,0 부터 15,15 까지 모두 저장이 가능합니다.

<br>

2. 따라서 행과 열을 다음과 같이 구성될 수 있습니다.

    - 행 0: q[15], q[14], ..., q[1], q[0]
    - 행 1: q[31], q[30], ..., q[17], q[16]
    - ...
    - 행 15: q[255], q[254], ..., q[241], q[240]

<br>

3. 가운데를 제외한 8곳의 이웃을 찾기 위해 `for` 를 통해 자신을 제외한 모든 것을 찾습니다.

<br>

4. 트로이드 경계처리를 위해 다음을 사용합니다.

    - 중심이 `(0,0)` 일 때를 기준으로 값을 구해봅니다.

        1. 16 x 16 : row , col 을 사용합니다.
        
        2.  3 x 3  : i , j 을 사용합니다.

    <br>

    - 본인`(0,0)`을 기준으로 왼쪽 위는 `(-1, -1)` 오른쪽 아래는 `(1,1)` 로 가정하고 구해보겠습니다.
    
    <br>

    -  `(-1,-1)` 좌표에 있는 15,15 을 구하기 위해서는 `row` ,`col` 에는 `0` 이 들어가고 `i` , `j` 에는 `-1`이 들어갑니다.

    <br>

    - 이를 활용해서 (15,15) 라는 위치를 구하려면 다음과 같이 계산됩니다. 
        - row + i  
        -  0  + -1 
    
    <br>
    
    - 이 배열은 16 x 16 이기 때문에 16 이라는 값을 더해주어 위치를 정할 수 있습니다. 따라서 다음과 같이 공식이 완성됩니다.
        - row +  i + 16 = 
        -  0  + -1 + 16 = 15

    <br>

    - 하지만 이 공식은 문제가 있습니다. 만약 본인이 `(15,15)` 좌표에 있고 `(1,1)` 의 이웃을 구한다고 가정하면 다음과 같이 값이 나오게 됩니다.
        - row +  i + 16 = 
        -  15 +  1 + 16 = 32

    <br>

    - 즉 최대 값이 32까지 나올 수 있고 이는 16 x 16에 어긋나기 때문에 최종적으로 % 16해주어야 맞는 값을 구할 수 있습니다.
        - (row +  i + 16) % 16 = 
        - ( 15 +  1 + 16) % 16 = 0 

    <br>


    - 이 조건들을 다 종합하면 다음과 같이 구현할 수 있습니다.
        ```verilog
        always@(*) begin
            n_q = 256'b0; // next_q 초기화 

            for( row = 0; row < 16; row++) begin // 16 x 16 배열 for문으로 선언
                for( col = 0; col < 16; col++ ) begin
                    idx = row * 16 + col; // 본인 위치 1차원 인덱스
                    neighbors = 0; // 이웃의 총 합 초기화

                    for( i = -1; i<= 1; i++ ) begin // 이웃의 3 x 3 배열 for문으로 선언
                        for( j = -1; j<=1; j++) begin
                            if(!( i == 0 && j == 0)) begin // 자신이 아닐 때
                                n_r = (row + i + 16) % 16; // 이웃의 위치 row
                                n_c = (col + j + 16) % 16; // 이웃의 위치 col
                                neighbors = neighbors + r_q[n_r * 16 + n_c]; // 이웃의 1 ,0 체크 후 합산
                            end
                        end
                    end
        ```

<br>

5. 이웃 상태를 구합니다.
    
    <br>

    - `neighbors` 로 주변의 살아있는 셀의 합계를 구합니다.

    - 각 셀은 `0` 또는 `1` 이기 때문에 최소 `0`에서 최대 `8`의 합계를 구할 수 있습니다.

<br>

6. 값을 구하기 위해 `본인 셀 + 이웃 셀`로 계산을 진행합니다.
    
    <br>

    - 만약 본인 셀이 `1`이라면 :
        <br>
        - 본인(1) + 이웃 0 또는 1이 (1) = 0>

        - 본인(1) + 이웃 2 또는 3이 (1) = 1
        
        - 본인(1) + 이웃 4 이상이 (1) = 0
    
    <br>
    
    - 만약 본인 셀이 `0`이라면 :
        <br>
        - 본인(0) + 이웃이 3 (1) = 1

        - 본인(0) + 그외 (1) = 0

    <br>

    - 이 조건을 다 종합하면 다음과 같이 구현할 수 있습니다.
        ```verilog
        if(r_q[idx] == 1'b1) begin  // 만약 본인이 1이라면 
            n_q[idx] = (neighbors == 2 || neighbors == 3) ? 1'b1 : 1'b0; 
        end else begin
            n_q[idx] = (neighbors == 3) ? 1'b1 : 1'b0;
        end
        ```
<br>

```verilog
module top_module(
    input clk,
    input load,
    input [255:0] data,
    output [255:0] q ); 

    reg [255:0] r_q; 
    reg [255:0] n_q;

    integer row, col, i, j, n_r, n_c, idx ,neighbors;

    always@(posedge clk) begin      
        if(load) begin
            r_q <= data;
        end else begin
            r_q <=n_q;
        end
    end


    always@(*) begin
        n_q = 256'b0;

        for( row = 0; row < 16; row++) begin
            for( col = 0; col < 16; col++ ) begin
                idx = row * 16 + col;
                neighbors = 0;

                for( i = -1; i<= 1; i++ ) begin
                    for( j = -1; j<=1; j++) begin
                        if(!( i == 0 && j == 0)) begin
                            n_r = (row + i + 16) % 16;
                            n_c = (col + j + 16) % 16;
                            neighbors = neighbors + r_q[n_r * 16 + n_c];
                        end
                    end
                end

                if(r_q[idx] == 1'b1) begin
                    n_q[idx] = (neighbors == 2 || neighbors == 3) ? 1'b1 : 1'b0;
                end else begin
                    n_q[idx] = (neighbors == 3) ? 1'b1 : 1'b0;
                end
            end
        end
    end

    assign q = r_q;

endmodule
```




#### 다음에 계속

---

